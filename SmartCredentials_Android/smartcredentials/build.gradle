plugins {
    id 'com.android.library'
}

ext {
    PUBLISH_GROUP_ID = 'com.onedevapp'
    PUBLISH_ARTIFACT_ID = 'SmartCredentialsPlugin'
    PUBLISH_VERSION = '1.2'
}

android {
    namespace 'com.onedevapp.SmartCredentialsPlugin'
    compileSdk 34

    defaultConfig {
        minSdk 21

        versionCode 3
        versionName "1.2"

        libraryVariants.all { variant ->
            def variantName = variant.name.capitalize()
            def copyTaskName = "copy${variantName}Artifacts"
            def assembleTaskName = "assemble${variantName}"
            task(copyTaskName, type: Copy, dependsOn: assembleTaskName, group: "build") {
                variant.outputs.each { output ->
                    def newOutputName = output.outputFile.name.replace(".aar", "-" + android.defaultConfig.versionName + ".aar")
                    from(output.outputFile.parent) {
                        include output.outputFile.name
                        rename output.outputFile.name, newOutputName
                    }
                }
                //into project.buildDir
                into(layout.buildDirectory.dir("../../../release"))
            }
            project.tasks.getByName(assembleTaskName).finalizedBy(copyTaskName)
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compileOnly files('libs/classes.jar')
    compileOnly 'com.google.android.gms:play-services-auth:20.7.0'
    compileOnly 'com.google.android.gms:play-services-auth-api-phone:18.0.1'

}